//*********************************************************
//
// Copyright (c) Microsoft. All rights reserved.
// This code is licensed under the MIT License (MIT).
// THIS CODE IS PROVIDED *AS IS* WITHOUT WARRANTY OF
// ANY KIND, EITHER EXPRESS OR IMPLIED, INCLUDING ANY
// IMPLIED WARRANTIES OF FITNESS FOR A PARTICULAR
// PURPOSE, MERCHANTABILITY, OR NON-INFRINGEMENT.
//
//*********************************************************
#include "MeshletCommon.hlsli"

// The groupshared payload data to export to dispatched mesh shader threadgroups
groupshared Payload s_Payload;

bool IsVisible(CullData c, float4x4 world, float scale, float3 viewPos)
{
	if ((Instance.Flags & CULL_FLAG) == 0) return true;

	// Do a cull test of the bounding sphere against the view frustum planes.
	const float4 center = mul(float4(c.BoundingSphere.xyz, 1.0), world);
	const float radius = c.BoundingSphere.w * scale;

	for (uint i = 0; i < 6; ++i)
		if (dot(center, Constants.Planes[i]) < -radius) return false;

	// Do normal cone culling
	if (IsConeDegenerate(c)) return true; // Cone is degenerate - spread is wider than a hemisphere.

	// Unpack the normal cone from its 8-bit uint compression
	const float4 normalCone = UnpackCone(c.NormalCone);

	// Transform axis to world space
	const float3 axis = normalize(mul(normalCone.xyz, (float3x3)world));

	// Offset the normal cone axis from the meshlet center-point - make sure to account for world scaling
	const float3 apex = center.xyz - axis * c.ApexOffset * scale;
	const float3 view = normalize(viewPos - apex);

	// The normal cone w-component stores -cos(angle + 90 deg)
	// This is the min dot product along the inverted axis from which all the meshlet's triangles are backface
	if (dot(view, -axis) > normalCone.w) return false;

	// All tests passed - it will merit pixels
	return true;
}

[NumThreads(AS_GROUP_SIZE, 1, 1)]
void main(uint gtid : SV_GroupThreadID, uint dtid : SV_DispatchThreadID, uint gid : SV_GroupID)
{
	bool visible = false;

	// Check bounds of meshlet cull data resource
	if (dtid < MeshInfo.MeshletCount)
		// Do visibility testing for this thread
		visible = IsVisible(MeshletCullData[dtid], Instance.World, Instance.Scale, Constants.CullViewPosition);

	// Compact visible meshlets into the export payload array
	if (visible)
	{
		const uint index = WavePrefixCountBits(visible);
		s_Payload.MeshletIndices[index] = dtid;
	}

	// Dispatch the required number of MS threadgroups to render the visible meshlets
	const uint visibleCount = WaveActiveCountBits(visible);
	DispatchMesh(visibleCount, 1, 1, s_Payload);
}
